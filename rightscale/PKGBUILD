# Maintainer: Brian Szmyd <brian.szmyd@rightscale.com>
# Contributor: Chris Fordham <chris.fordham@rightscale.com>

pkgname=rightscale
pkgver=5.8.13
pkgrel=4
pkgdesc='RightScale RightLink cloud instance agent.'
arch=(i686 x86_64)
url=http://rightscale.com/
license=(MIT RUBY)
depends=(bash dnsutils lsb-release sudo openssh curl shadow rightscale-cloud rightscale-motd rightscale-monit)
makedepends=(autoconf git)
conflicts=(rightscale-deb)
options=(emptydirs strip !docs !libtool !staticlibs )
install=rightscale.install

_gitname=right_link
_rl_prefix="/opt/rightscale"

# We also install ruby, so here's some details
_sandbox_dir="${_rl_prefix}/sandbox"
_rb_gitname=ruby
_rb_pkgver=1.8.7-p371
_rb_gitdir="${_rb_gitname}-${_rb_pkgver}"
_rg_gitname=rubygems
_rg_pkgver=1.6.2
_rg_gitdir="${_rg_gitname}-${_rg_pkgver}"

source=(
	"${_gitname}::git://github.com/rightscale/right_link.git"
	"http://ftp.ruby-lang.org/pub/ruby/1.8/${_rb_gitname}-${_rb_pkgver}.tar.gz"
	"http://production.cf.rubygems.org/rubygems/${_rg_gitname}-${_rg_pkgver}.tgz"
	${install}
	rightscale_functions
	system_configurator.rb.patch
	LICENSE)
sha256sums=('SKIP'
            'e60a322f8f2a616eba01651f5ab620e7e48e4f8adfe711aec61cc74a91d54d3c'
            'cb5261818b931b5ea2cb54bc1d583c47823543fcf9682f0d6298849091c1cea7'
            '364a255bc0958ca536f1eb5c15fac082df9d5e42013062078f08e4935f0668ee'
            '2feb50136512c1563234e415c24e17bda02413b5c5320ee2c371e1c6d23ae127'
            'e5cf8534dc98fd4c6e02d90ebd38eb30ce44613226d8372be4698fefb651b50c'
            '30dadf1285b4d1bc8b3b7951de23c20482e215fc34fcf898a8b05024c1cb3faf')		 # generate with 'makepkg -g'

ruby_cmd() {
  # These are some LOAD_PATHs that need to be manually added to ruby
  # since it's not installed in the /opt yet.
  local lib_paths=$(for path in ${ruby_libs[@]}; do echo "-I${ruby_dir}/lib/ruby/${path}"; done)

  RUBYOPT="${lib_paths}" \
    ${ruby_dir}/bin/ruby ${@}
}

build() {
  # Build Ruby
  pushd ${_rb_gitdir}
    autoconf
    ./configure \
      --prefix="${_sandbox_dir}" \
      --enable-pthread
    make
  popd
}

package() {
  ruby_dir=${pkgdir}/${_sandbox_dir}
  ruby_libs=()
  local right_link_dir=${pkgdir}/${_rl_prefix}/right_link
  local license_dir=${pkgdir}/usr/share/license/rightscale

  for new_dir in ${license_dir} ${right_link_dir}; do
    mkdir -p ${new_dir}
  done

  # Install Ruby
  pushd ${_rb_gitdir}
    make DESTDIR=${pkgdir} install
    ruby_libs+=(1.8 1.8/${CARCH}-linux)
  popd

  # Install RubyGems
  pushd ${_rg_gitdir}
    ruby_cmd ./setup.rb --no-ri --no-rdoc
    ruby_libs+=(site_ruby/1.8)
    sed -i "s@${pkgdir}@@" ${ruby_dir}/bin/gem
    ruby_cmd ${ruby_dir}/bin/gem install bundle --no-rdoc --no-ri
  popd

  # Install RightLink
  pushd ${_gitname}
    git archive --format=tar v${pkgver} | tar -C ${right_link_dir} -x
  popd

  pushd ${right_link_dir}
    ruby_cmd ${ruby_dir}/lib/ruby/gems/1.8/bin/bundle install
    mv ./LICENSE ${license_dir}/license.txt
  popd

  msg 'Add rightscale_functions to package.'
  install -D "$startdir/rightscale_functions" "$pkgdir/opt/rightscale/bin/rightscale_functions"

  cd "$pkgdir"
  msg "Applying patch for system_configurator.rb..."
  patch -p0 < "$startdir/system_configurator.rb.patch"

  msg 'Adding LICENSE to package...'
  install -D "$startdir/LICENSE" "$pkgdir/opt/rightscale/LICENSE"
}

# vim:syntax=sh